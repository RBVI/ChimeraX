; Create ChimeraX installer

#define VCmsg "Installing newer Microsoft Visual C++ Runtime...."

[Code]
const
    // Extract version of VC_redist.exe by looking at the File Version
    // in the Details tab of the Properties dialog.
    // For example: 14.25.28508.3 -> NeededMajor: 14, NeededMinor: 25
    NeededMajor = 14;
    NeededMinor = 42;
    RuntimeKey = 'SOFTWARE\Microsoft\VisualStudio\14.0\VC\Runtimes\x64';

function ShouldInstallVCRuntime: Boolean;
    // Return True if Visual C++ 2015-2019 runtime should be installed
var
    major: Cardinal;
    minor: Cardinal;
begin
    Result := True;
    if RegQueryDWordValue(HKEY_LOCAL_MACHINE, RuntimeKey, 'Major', major) then begin
        if RegQueryDWordValue(HKEY_LOCAL_MACHINE, RuntimeKey, 'Minor', minor) then begin
            Result := (major < NeededMajor) or ((major = NeededMajor) and (minor < NeededMinor));
        end;
    end;
end;

function NextButtonClick(CurPageID: Integer): Boolean;
var
    path: String;
begin
    Result := True;
    if CurPageID = wpSelectDir then begin
        path := ExpandConstant('{app}\bin\ChimeraX.exe');
        if FileExists(path) then begin
            MsgBox('There is already a version of ChimeraX installed in '
                + ExpandConstant('{app}') + '.  Please uninstall that '
                + 'version or select a different folder.',
                mbInformation, MB_OK);
            Result := False;
        end;
    end;
end;

[Setup]
ArchitecturesAllowed=x64compatible
ArchitecturesInstallIn64BitMode=x64compatible
AppCopyright=Copyright © 2016-2025 Regents of the University of California
AppId=UCSF ChimeraX $(VERSION)
AppName=ChimeraX
AppVersion=$(VERSION)
AppVerName=UCSF ChimeraX $(VERSION)
AppPublisher=University of California at San Francisco
AppPublisherURL=http://www.rbvi.ucsf.edu/chimerax
AppSupportURL=http://www.rbvi.ucsf.edu/chimerax/docs/contact.html
AppUpdatesURL=http://www.rbvi.ucsf.edu/chimerax/download.html
Compression=lzma2/ultra64
SolidCompression=yes
LZMAUseSeparateProcess=yes
LZMANumBlockThreads=4
UsePreviousAppDir=no
DefaultDirName={pf}\ChimeraX $(VERSION)
DefaultGroupName=UCSF ChimeraX $(VERSION)
DisableStartupPrompt=yes
DisableFinishedPage=no
ExtraDiskSpaceRequired=20000000
; LicenseFile=docs\license.txt
OutputBaseFilename=chimerax-installer
OutputDir=$(BUILD_ROOT)
PrivilegesRequired=none
; SignTool=byparam signtool.exe sign /f $q$(HERE)/CodeSign.pfx$q /p CODESIGN /fd sha1 /t http://timestamp.comodoca.com/authenticode /d $qUCSF ChimeraX$q $f
; SignTool=byparam signtool.exe sign /f $(HERE)/CodeSign.pfx /p CODESIGN /as /fd sha256 /td sha256 /tr http://timestamp.comodoca.com/rfc3161 /d $qUCSF ChimeraX$q $f
SourceDir=$(BUILD_ROOT)
UninstallDisplayIcon={app}\bin\share\ChimeraX.ico,0
WizardImageFile=$(HERE)\chimerax-wizard.bmp
WizardImageStretch=no
WizardSmallImageFile=$(HERE)\chimerax-wizard-small.bmp
WizardImageAlphaFormat=defined
WizardStyle=modern
RestartIfNeededByRun=no

[Messages]
; Replace the "stop all other applications" message
WelcomeLabel2=This will install [name/ver] on your computer.%n%nPlease remove previous versions prior to continuing with installation.

[Tasks]
Name: "chimeraxicon"; Description: "Create a ChimeraX &desktop icon"; GroupDescription: "Additional icons:"
Name: "quickchimeraxicon"; Description: "Create a ChimeraX &Quick Launch icon"; GroupDescription: "Additional icons:"; Flags: unchecked

[Dirs]
Name: {commonappdata}\ChimeraX

[Files]
Source: "C:\Program Files\Microsoft Visual Studio\2022\Enterprise\VC\Redist\MSVC\v143\vc_redist.x64.exe"; DestDir: "{tmp}"
; TODO: Send the rest of the app to {app} instead of {app}\bin
Source: "ChimeraX.app\bin\*"; DestDir: "{app}\bin"; Excludes: "__pycache__"; Flags: ignoreversion recursesubdirs
; Add other non-empty directories as needed
; Source: "ChimeraX.app\lib\*"; DestDir: "{app}\lib"; Flags: ignoreversion recursesubdirs
; Source: "ChimeraX.app\include\*"; DestDir: "{app}\include"; Flags: ignoreversion recursesubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
; Source: "$(WindowsSdkDir)\Bootstrapper\Packages\vcredist_x86\vcredist_x86.exe"

[INI]
Filename: "{app}\chimerax.url"; Section: "InternetShortcut"; Key: "URL"; String: "http://www.cgl.ucsf.edu/chimerax/"

[Registry]
Root: HKCU; Subkey: "Software\Microsoft\Windows\CurrentVersion\Explorer\MenuOrder\Start Menu2\Programs\{groupname}"; Flags: deletekey
Root: HKCU; Subkey: "Software\Microsoft\Windows\CurrentVersion\Explorer\MenuOrder\Start Menu2\Programs\{groupname}"; Flags: uninsdeletekey

[Icons]
Name: "{group}\ChimeraX $(VERSION)"; Filename: "{app}\bin\ChimeraX.exe"; WorkingDir: "{commonappdata}\ChimeraX"
Name: "{group}\ChimeraX - Debug"; Filename: "{app}\bin\ChimeraX-console.exe"; Parameters: "--debug"; WorkingDir: "{commonappdata}\ChimeraX"
Name: "{group}\ChimeraX Home Page"; Filename: "{app}\chimerax.url"
Name: "{group}\Uninstall ChimeraX"; Filename: "{uninstallexe}"
Name: "{commondesktop}\ChimeraX $(VERSION)"; Filename: "{app}\bin\ChimeraX.exe"; Tasks: chimeraxicon; WorkingDir: "{commondesktop}"
Name: "{commonappdata}\Microsoft\Internet Explorer\Quick Launch\ChimeraX $(VERSION)"; Filename: "{app}\bin\ChimeraX.exe"; Tasks: quickchimeraxicon; WorkingDir: "{commonappdata}\ChimeraX"

[Run]
Filename: "{tmp}/Vc_redist.x64.exe"; Parameters: "/install /passive /norestart"; StatusMsg: "{#VCmsg}"; Check: ShouldInstallVCRuntime
Filename: "{app}\bin\python.exe"; Description: "Precompile Python Modules"; Parameters: "-m compileall -q ""{app}"""; StatusMsg: "Precompiling Python Modules..."; Flags: postinstall runascurrentuser runhidden
Filename: "{app}\bin\ChimeraX.exe"; Description: "Start ChimeraX"; WorkingDir: "{commonappdata}\ChimeraX"; Flags: postinstall unchecked nowait runasoriginaluser

[UninstallDelete]
Type: files; Name: "{app}\chimerax.url"

[UninstallRun]
; Remove compiled Python modules (__pycache__ directories)
Filename: "{app}\bin\python.exe"; Parameters: """{app}\bin\remove_pycache.py"" ""{app}"""; StatusMsg: "Removing Precompiled Modules..."; Flags: runascurrentuser runhidden
