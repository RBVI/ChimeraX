(* vi: set expandtab shiftwidth=4 softtabstop=4: *)

(*
 * === UCSF ChimeraX Copyright ===
 * Copyright 2016 Regents of the University of California.
 * All rights reserved.  This software provided pursuant to a
 * license agreement containing restrictions on its disclosure,
 * duplication and use.  For details see:
 * http://www.rbvi.ucsf.edu/chimerax/docs/licensing.html
 * This notice must be embedded in or attached to all copies,
 * including partial copies, of the software or any revisions
 * or derivations thereof.
 * === UCSF ChimeraX Copyright ===
 *)

(* Remember to use left recursion when needed, not right *)

atom_specifier   = left:as_term operator:'&' right:atom_specifier
                 | left:as_term operator:'|' right:atom_specifier
                 | left:as_term
                 ;

as_term          = '(' atomspec:atom_specifier ')' [zone:zone_selector]
                 | '~' tilde:as_term [zone:zone_selector]
                 | selector:selector_name [zone:zone_selector]
                 | models:model_list
                 ;

selector_name    = name:/[a-zA-Z][-+a-zA-Z0-9]*/
                 ;

model_list       = { model+:model }+
                 ;

model            = '#' hierarchy:model_hierarchy [ '##' attrs:attribute_list ] [ parts:model_parts ] [ zone:zone_selector ]
                 | '##' attrs:attribute_list [ parts:model_parts ] [ zone:zone_selector ]
		 | parts:model_parts [ zone:zone_selector ]
                 ;

model_hierarchy  = range_list:model_range_list { /(?<!\s)\./ hierarchy:model_hierarchy }*
                 ;

model_range_list = range:model_range { ',' range_list:model_range_list }*
                 ;

model_range      = start:model_spec '-' end:model_spec
                 | start:model_spec
                 ;

(* limit model numbers to 5 digits to avoid conflict with # colors *)

model_spec       = number:/[0-9]{1,5}(?![0-9A-Fa-f])/
                 | star:'*'
                 ;

model_parts      = { chain+:chain }+
                 ;

chain            = '/' parts:part_list [ '//' attrs:attribute_list ] { residue+:residue }*
                 | '//' attrs:attribute_list { residue+:residue }*
		 | { residue+:residue }+
                 ;

residue          = ':' parts:part_list [ '::' attrs:attribute_list ] { atom+:atom }*
                 | '::' attrs:attribute_list { atom+:atom }*
		 | { atom+:atom }+
                 ;

atom             = '@' parts:part_list [ '@@' attrs:attribute_list ]
                 | '@@' attrs:attribute_list
                 ;

part_list        = range:part_range_list ',' part:part_list
                 | range:part_range_list 
                 ;

part_range_list  = start:/-?[a-zA-Z0-9'"]+/ [ '-' end:/[-a-zA-Z0-9'"]+/ ]
                 ;

attribute_list   = attr_test { ',' attr_test }*
                 ;

attr_test        = name:attr_name op:attr_operator value:attr_value
                 | name:attr_name
                 | no:'~' name:attr_name
                 ;

attr_name        = /[a-zA-Z_][a-zA-Z0-9_]*/
                 ;

attr_operator    = '>='
                 | '>'
                 | '<='
                 | '<'
                 | '='
                 | '!='
                 | '<>'
                 ;

attr_value       = ?/[^,#/:@ \t]+/?
                 ;

zone_selector    = zone_operator real_number
                 ;

zone_operator    = '@>'
                 | '@<'
                 | ':>'
                 | ':<'
                 | '/>'
                 | '/<'
                 | '#>'
                 | '#<'
		 ;

real_number      = /[0-9]*\.?[0-9]+/
                 ;
